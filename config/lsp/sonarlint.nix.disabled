{
  pkgs,
  lib,
  module ? {},
  ...
}: let
  cfg = module.config.sonarlint or {};

  filetypes = ["javascript" "typescript" "javascriptreact" "typescriptreact" "go" "html"];

  analyzers = ["sonarjs" "sonarpython" "sonarhtml" "sonargo" "sonartext"];
  analyzerPaths = map (analyzer: "${pkgs.sonarlint-ls}/share/plugins/${analyzer}.jar") analyzers;

  cmd = lib.flatten [
    (lib.getExe pkgs.sonarlint-ls)
    "-stdio"
    "-analyzers"
    analyzerPaths
  ];
in {
  vim.extraPackages = [
    pkgs.sonarlint-ls
    pkgs.nodejs_24
  ];

  vim.lazy.plugins.sonarlint-nvim = {
    package = pkgs.sonarlint-nvim;
    before = ''
      local lspconfig = require("lspconfig")
    '';
    enabled = lib.mkIf (cfg.connectedMode.enable or false) (lib.generators.mkLuaInline
      /*
      lua
      */
      ''
        function()
          local current_dir = vim.fn.getcwd()
          local projects = ${lib.generators.toLua {} cfg.connectedMode.projects}
          local project = projects[current_dir]


          for project_path, _ in pairs(projects) do
            local expanded_path = vim.fn.expand(project_path)
            if vim.startswith(current_dir, expanded_path) then
              return true
            end
          end

          return false
        end
      '');
    ft = filetypes;
    setupModule = "sonarlint";
    setupOpts = {
      filetypes = filetypes;
      server = {
        cmd = cmd;
        settings.sonarlint.connectedMode.connections = {
          sonarqube = cfg.connectedMode.connections.sonarqube or [];
        };
        before_init =
          lib.generators.mkLuaInline
          /*
          lua
          */
          ''
            function(params, config)
              local projects = ${lib.generators.toLua {} cfg.connectedMode.projects or {}}
              local project = projects[params.rootPath]

              config.settings.sonarlint.connectedMode.project = {
                connectionId = project.connectionId,
                projectKey = project.projectKey,
              };
            end
          '';
      };
    };
  };
}
